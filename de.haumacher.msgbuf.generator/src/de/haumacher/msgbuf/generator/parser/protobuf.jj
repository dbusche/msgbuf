/**
 * JavaCC Grammar File
 */
options
{
    JDK_VERSION = "1.8";
    STATIC = false;
    UNICODE_INPUT = true;
    JAVA_UNICODE_ESCAPE = false;
}

PARSER_BEGIN(ProtobufParser)
package de.haumacher.msgbuf.generator.parser;

import de.haumacher.msgbuf.generator.Util;
import de.haumacher.msgbuf.generator.ast.*;

/**
 * Parser for protobuf definition files..
 */
public class ProtobufParser {
   
}
PARSER_END(ProtobufParser)

<DEFAULT>

SKIP :
{
  < SPACE :  " " >
| < TAB : "\t" >
| < CR : "\r" >
| < NL : "\n" >
| < BLOCK_COMMENT : "/*" ~["*"] ( ~[ "*" ] | "*" ~[ "/" ] )* "*/" | "/**/" >
| < LINE_COMMENT : "//" ( ~[ "\r", "\n" ] )* [ "\r", "\n" ] >
}

<DEFAULT>

TOKEN :
{
  < PACKAGE : "package" >
| < SYNTAX : "syntax" >
| < MESSAGE : "message" >
| < ENUM : "enum" >
| < MAP : "map" >
| < ONEOF : "oneof" >
| < REPEATED : "repeated" >
| < TRANSIENT : "transient" >
| < OPTION : "option" >
| < EXTENDS : "extends" >
| < ABSTRACT : "abstract" >
| < RESERVED : "reserved" >
| < TO : "to" >
| < MAX : "max" >
| < TRUE : "true" >
| < FALSE : "false" >
| < INT_TYPE : "int" >
| < LONG_TYPE : "long" >
| < BOOLEAN_TYPE : "boolean" >
| < TYPE :
    "double" |
    "float" |
    "int32" |
    "uint32" |
    "sint32" |
    "fixed32" |
    "sfixed32" |
    "int64" |
    "uint64" |
    "sint64" |
    "fixed64" |
    "sfixed64" |
    "bool" |
    "string" |
    "bytes"
  >
| < NUMBER : ("0" | ( [ "1"-"9" ] ([ "0"-"9" ])* )) ("." ([ "0"-"9" ])+)? >
| < STRING : "'" (~[ "'", "\\" ] | < QUOTE > )* "'" | "\"" (~[ "\"", "\\" ] | < QUOTE > )* "\"" >
| < XNAME : "`" (~[ "`" ] )+ "`" >
| < #QUOTE : "\\\\" | "\\'" | "\\\"" >
| < NAME : [ "A"-"Z", "a"-"z", "_" ] ([ "A"-"Z", "a"-"z", "_", "0"-"9"])* >
| < DOC_COMMENT : "/**" ( ~[ "*" ] | "*" ~[ "/" ] )* "*/" | "/**/" >
}

DefinitionFile file() :
{
  DefinitionFile result = DefinitionFile.definitionFile();
  QName pkgName;
  Definition def;
  Token comment;
}
{
  (    < SYNTAX > "=" < STRING > ";"
  )?
  
  (
    < PACKAGE > pkgName = qName() ";"
    {
		result.setPackage(pkgName);
    }
  )?
  
  (    option()
  )*
  
  (    (
      comment = < DOC_COMMENT >
    |
      { comment = null; } 
    )
    def = definition()
    {
      if (comment != null) {
        def.setComment(Util.stripComment(comment.image));
      }
      result.addDefinition(def);
    }
  )*
  {
    return result;
  }
}

void option() :
{
}
{
  < OPTION > < NAME > "=" value() ";"
}

void value() :
{
}
{
  (    < STRING >
  | < NUMBER >
  | < TRUE >
  | < FALSE >
  )
}

Definition definition() :
{
  Definition result;
}
{
  (    result = message()
  | result = enumeration()
  )
  {
    return result;
  }
}

EnumDef enumeration() :
{
  EnumDef result = EnumDef.enumDef();
  String name;
  Constant constant;
}
{
  < ENUM > name = name() "{"
  {
    result.setName(name);
  }
  (
    option()
  )*
  (
    constant=constant()
    {
      result.addConstant(constant);
    }
  | reserved()
  )*
  "}"
  {
    return result;
  }
}

Constant constant() :
{
  Constant result = Constant.constant();
  String name;
  Token index;
  Token comment;
}
{
  (
    comment=< DOC_COMMENT >
    { result.setComment(Util.stripComment(comment.image)); }
  )?
  name=name()
  {
    result.setName(name);
  }
  (
    "=" index=< NUMBER >
    {
      result.setIndex(Integer.parseInt(index.image));
    }
  )?
  ";"
  {
    return result;
  }
}

MessageDef message() :
{
  MessageDef result = MessageDef.messageDef();
  String name;
  QName generalization;
  Definition def;
  Field field;
}
{
  (    < ABSTRACT >
    {
      result.setAbstract(true);
    }
  )?
  < MESSAGE > name=name()
  {
    result.setName(name);
  }
  (
    < EXTENDS > generalization=qName()
    {
		result.setExtends(generalization);
    }
  )?
  "{"
  (
    field=field()
    {
      result.addField(field);
    }
  | oneof()
  | reserved()
  | def = definition()
    {
      result.addDefinition(def);
    } 
  )*
  "}"
  {
    return result;
  }
}

Field field() :
{
  Field result = Field.field();
  Type type;
  String name;
  Token index;
  Token comment;
}
{
  (
    comment=< DOC_COMMENT >
    { result.setComment(Util.stripComment(comment.image)); }
  )?
  (
    < TRANSIENT >
    {
      result.setTransient(true);
    }
  )?
  (
    < REPEATED >
    {
      result.setRepeated(true);
    }
  )?
  type=type()
  {
    result.setType(type);
  }
  name=name()
  {
    result.setName(name);
  }
  (
    "=" index=< NUMBER >
    {
      result.setIndex(Integer.parseInt(index.image));
    }
  )?
  ";"
  {
    return result;  }}

void reserved() :
{
}
{
 < RESERVED > reservation() ( "," reservation() )* ";"
}

void reservation() : 
{
}
{
  (
    < NUMBER > ( < TO > ( < NUMBER > | < MAX > ) )?
  | < STRING >
  )
}

void oneof() :
{
}
{
  < ONEOF > < NAME > "{"
	(	  type() < NAME > ( "=" < NUMBER > )? ";"
	)+
  "}"
}

Type type() :
{
  Type result;
}
{
  (    result=primitiveType()
  | result=customType()
  | result=map()
  )
  {
    return result;
  }
}

PrimitiveType primitiveType() :
{
  Token t;
  PrimitiveType result;
}
{
  (
	t=< TYPE >    { result = PrimitiveType.primitiveType().setKind(PrimitiveType.Kind.valueOf(t.image.toUpperCase())); }
  | t=< INT_TYPE >
    { result = PrimitiveType.primitiveType().setKind(PrimitiveType.Kind.INT32); }
  | t=< LONG_TYPE >
    { result = PrimitiveType.primitiveType().setKind(PrimitiveType.Kind.INT64); }
  | t=< BOOLEAN_TYPE >
    { result = PrimitiveType.primitiveType().setKind(PrimitiveType.Kind.BOOL); }
  )
  { return result; }
}

CustomType customType() :
{
  QName name;  
}
{
  name=qName()
  {
    return CustomType.customType().setName(name);
  }
}

MapType map() :
{
  Type keyType;
  Type valueType;
}
{
  < MAP > "<" keyType=type() "," valueType=type() ">"
  {
    return MapType.mapType().setKeyType(keyType).setValueType(valueType);
  }
}

QName qName() :
{
  QName result = QName.qName();
  String name;
}
{
  name = name()
  {
    result.addName(name);
  }
  (
    "." name = name()
    {
      result.addName(name);
    }
  )*
  {
    return result;
  }
}

String name() :
{
  Token t;
  String result;
}
{
  (
    t = < NAME >
    { result = t.image; }
  | t = < TO >
    { result = t.image; }
  | t = < MAX >
    { result = t.image; }
  | t = < XNAME >
    { result = t.image.substring(1, t.image.length() - 1); }
  )
  {
    return result;
  }
}

